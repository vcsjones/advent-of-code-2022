IDENTIFICATION DIVISION. *> Compile and run with cobc -std=cobol2014 --free -x day2.cbl && ./day2
PROGRAM-ID. DAY2.

ENVIRONMENT DIVISION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT DAY2DATA ASSIGN TO INPUT
    ORGANIZATION IS LINE SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD DAY2DATA.
    01 STRATEGY.
        05 RESULT PIC X(3).

WORKING-STORAGE SECTION.
77 WS-EOF PIC A(1).
77 WS-PART1-SUM PIC 9(9).
77 WS-PART2-SUM PIC 9(9).
77 WS-INDEX-1 PIC 9(1).
77 WS-INDEX-2 PIC 9(1).
77 WS-SCORE PIC 9(1).
01 WS-STRATEGY.
    05 WS-RESULT-LEFT PIC X(1).
    05 WS-BLANK PIC X(1).
    05 WS-RESULT-RIGHT PIC X(1).
01 WS-PART1-LOOKUP-TABLE.
    05 WS-PART1-LOOKUP-TABLE-RESULT OCCURS 3 TIMES.
        10 WS-PART1-LOOKUP OCCURS 3 TIMES.
            15 Z PIC 9(1).
01 WS-PART2-LOOKUP-TABLE.
    05 WS-PART2-LOOKUP-TABLE-RESULT OCCURS 3 TIMES.
        10 WS-PART2-LOOKUP OCCURS 3 TIMES.
            15 X PIC 9(1).

PROCEDURE DIVISION.
    MOVE '417852396' TO WS-PART1-LOOKUP-TABLE.
    MOVE '312456897' TO WS-PART2-LOOKUP-TABLE.

    OPEN INPUT DAY2DATA.
        PERFORM UNTIL WS-EOF='Y'
            READ DAY2DATA INTO WS-STRATEGY
                AT END MOVE 'Y' TO WS-EOF
                NOT AT END

                EVALUATE WS-RESULT-LEFT
                    WHEN 'A' MOVE 1 TO WS-INDEX-1
                    WHEN 'B' MOVE 2 TO WS-INDEX-1
                    WHEN 'C' MOVE 3 TO WS-INDEX-1
                END-EVALUATE

                EVALUATE WS-RESULT-RIGHT
                    WHEN 'X' MOVE 1 TO WS-INDEX-2
                    WHEN 'Y' MOVE 2 TO WS-INDEX-2
                    WHEN 'Z' MOVE 3 TO WS-INDEX-2
                END-EVALUATE

                MOVE WS-PART1-LOOKUP(WS-INDEX-2, WS-INDEX-1) TO WS-SCORE
                COMPUTE WS-PART1-SUM = WS-PART1-SUM + WS-SCORE

                MOVE WS-PART2-LOOKUP(WS-INDEX-2, WS-INDEX-1) TO WS-SCORE
                COMPUTE WS-PART2-SUM = WS-PART2-SUM + WS-SCORE
            END-READ
        END-PERFORM.

        DISPLAY 'PART 1: ' WS-PART1-SUM
        DISPLAY 'PART 2: ' WS-PART2-SUM
    CLOSE DAY2DATA.
    STOP RUN.
